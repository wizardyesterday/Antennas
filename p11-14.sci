//******************************************************************
// Problem P11-14
//******************************************************************

//*****************************************************************
// function declarations
//******************************************************************
function a = cin(x)
  v = 0:.01:x;
  v(1) = .0000001;
  i = (1 - cos(v)) ./ v;
  a = inttrap(v,i);
endfunction

function a = si(x)
  v = 0:.01:x;
  v(1) = .0000001;
  i = sin(v) ./ v;
  a = inttrap(v,i);
endfunction

function a = ci(x)
  a = 0.577 + log(x) - cin(x);
endfunction

function z11 = calcZ11(n)
  r11 = 30 * cin(2*%pi*n);
  x11 = 30 * si(2*%pi*n);
  z11 = r11 + %i*x11;
endfunction

function z21 = calcZ21(l,d)
  a1 = beta*d;
  a2 = beta*(sqrt(d*d + l*l) + l);
  a3 = beta*(sqrt(d*d + l*l) - l);
  r21 = 30*(2*ci(a1) - ci(a2) - ci(a3));
  x21 = -30*(2*si(a1) - si(a2) - si(a3));
  z21 = r21 + %i*x21;
endfunction

function x = calcMaxGain(r00,r11,r12)
  x = sqrt(2*r00/(r11-r12));
endfunction

//******************************************************************
// main code
//******************************************************************

//******************************************************************
// set parameters
//******************************************************************
n = 1;
d = 0.2;
l = 0.5;
beta = 2*%pi;

//******************************************************************
// compute resistance and reactance terms
//******************************************************************
z11 = calcZ11(n);
r11 = real(z11);

z12 = calcZ21(l,d);
r12 = real(z12);

//******************************************************************
// compute maximum gain
//******************************************************************
gMax = calcMaxGain(r11,r11,r12);
