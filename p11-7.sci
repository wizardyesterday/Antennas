//******************************************************************
// Problem P11-7
//******************************************************************

//*****************************************************************
// function declarations
//******************************************************************
function a = cin(x)
  v = 0:.01:x;
  v(1) = .0000001;
  i = (1 - cos(v)) ./ v;
  a = inttrap(v,i);
endfunction

function a = si(x)
  v = 0:.01:x;
  v(1) = .0000001;
  i = sin(v) ./ v;
  a = inttrap(v,i);
endfunction

function a = ci(x)
  a = 0.577 + log(x) - cin(x);
endfunction

function z11 = calcZ11(n)
  r11 = 30 * cin(2*%pi*n);
  x11 = 30 * si(2*%pi*n);
  z11 = r11 + %i*x11;
endfunction

function z21 = calcZ21(l,d)
  a1 = beta*d;
  a2 = beta*(sqrt(d*d + l*l) + l);
  a3 = beta*(sqrt(d*d + l*l) - l);
  r21 = 30*(2*ci(a1) - ci(a2) - ci(a3));
  x21 = -30*(2*si(a1) - si(a2) - si(a3));
  z21 = r21 + %i*x21;
endfunction

//******************************************************************
// main code
//******************************************************************

//******************************************************************
// set parameters
//******************************************************************
d12 = 0.5;
d13 = 1;
d14 = 1.5;
d15 = 2;
d16 = 2.5;
n = 1;
l = 0.5;
beta = 2*%pi;
i1 = 0.919;
i2 = 0.845;
i3 = 1;

//******************************************************************
// compute self and mutual impedances
//******************************************************************
z11 = calcZ11(n);
z12 = calcZ21(l,d12);
z13 = calcZ21(l,d13);
z14 = calcZ21(l,d14);
z15 = calcZ21(l,d15);
z16 = calcZ21(l,d16);

//******************************************************************
// compute driving point impedances
//******************************************************************
z1 = z11 + (i2/i1)*(z12 + z15) + (i3/i1)*(z13 + z14) + z16;
z2 = z11 + (i1/i2)*(z12 + z15) + (i3/i2)*(z12 + z13) + z14;
z3 = z11 + (i2/i3)*(z12 + z13) + (i1/i3)*(z13 + z14) + z12;
z4 = z3;
z5 = z2;
z6 = z1;
