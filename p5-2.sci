//******************************************************************
// Problem P5-2
//******************************************************************

//*****************************************************************
// function declarations
//******************************************************************
function x = E(l,phi)
  x1 = cos(%pi*l*cos(phi)) - cos(%pi*l);
  x2 = sin(phi);
  m = find(x2 == 0);
  x2(m) = 0.000001;
  x = x1 ./ x2;
endfunction

function x = calcRadiationResistance(l)
  u = -1:0.01:1;
  k = %pi*l;
  n = cos(k*u) - cos(k);
  n = n.* n;
  d = 1 - u.*u;
  m = find(d == 0);
  d(m) = 0.000001;
  i = n ./ d;
  x = inttrap(u,i);
  x = x * 60;
endfunction

//******************************************************************
// main code
//******************************************************************

//******************************************************************
// set parameters
//******************************************************************
phi = 0:%pi/100:2*%pi;
l = 2;

//******************************************************************
// construct fields
//******************************************************************
E1 = E(l,phi);

//******************************************************************
// compute radiation resistance at current loop
//******************************************************************
r0 = calcRadiationResistance(l)

//******************************************************************
// polar plot of field
//******************************************************************
x = abs(E1) .* cos(phi);
y = abs(E1) .* sin(phi);
plot2d(x,y);

